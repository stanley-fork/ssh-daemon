apply plugin: 'com.android.application'

android {
    compileSdk = 36

    namespace = 'com.sshdaemon'

    defaultConfig {
        applicationId "com.daemon.ssh"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 52
        versionName "2.1.34"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt',
                         'META-INF/LICENSE.txt',
                         'META-INF/NOTICE.txt',
                         'META-INF/NOTICE',
                         'META-INF/LICENSE',
                         'META-INF/DEPENDENCIES',
                         'META-INF/notice.txt',
                         'META-INF/license.txt',
                         'META-INF/dependencies.txt',
                         'META-INF/LGPL2.1',
                         'META-INF/services/javax.annotation.processing.Processor']
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }


    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            useJUnitPlatform()
        }
    }

    lint {
        baseline = file('lint-baseline.xml')
        abortOnError = false
        checkReleaseBuilds = false
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:deprecation"
    }
}

ext {
    sshdVersion = '2.16.0'
}

dependencies {
    api 'com.google.android.material:material:1.13.0'

    implementation "org.apache.sshd:sshd-core:${sshdVersion}"
    implementation "org.apache.sshd:sshd-sftp:${sshdVersion}"
    implementation "org.apache.sshd:sshd-contrib:${sshdVersion}"
    implementation "org.slf4j:slf4j-api:2.0.17"
    implementation "org.slf4j:slf4j-log4j12:2.0.17"
    implementation "org.bouncycastle:bcpkix-jdk15to18:1.82"
    implementation "net.i2p.crypto:eddsa:0.3.0"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.13.4"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.13.4"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.13.4"
    testImplementation "org.junit.platform:junit-platform-launcher:1.13.4"

    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "org.mockito:mockito-core:5.19.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.2.0"
    androidTestImplementation "androidx.test:core:1.7.0"
    androidTestImplementation "androidx.test.ext:junit:1.3.0"
    androidTestImplementation "androidx.test:runner:1.7.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.7.0"
}
